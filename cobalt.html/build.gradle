allprojects {
    group "org.hexworks.cobalt"
    version "2018.1.1-PREVIEW"
}

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.0-rc-190'
    }
}

apply plugin: 'kotlin-multiplatform'

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
        maven { url 'https://dl.bintray.com/kotlin/kotlinx' }
    }
}

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.js, 'web')

        configure([jvm]) {
            tasks.getByName(compilations.main.compileKotlinTaskName).kotlinOptions {
                jvmTarget = '1.8'
            }
            tasks.getByName(compilations.test.compileKotlinTaskName).kotlinOptions {
                jvmTarget = '1.8'
            }
        }

        configure([web]) {
            tasks.getByName(compilations.main.compileKotlinTaskName).kotlinOptions {
                sourceMap = true
            }
        }
    }

    sourceSets {
        commonMain {
            kotlin.srcDirs = ["core/src/main/kotlin"]
            resources.srcDirs = ["core/src/main/resources"]
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib'
                api group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: coroutines_version
            }
        }
        commonTest {
            kotlin.srcDirs = ["core/src/test/kotlin"]
            resources.srcDirs = ["core/src/test/resources"]
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }

        jvmMain {
            kotlin.srcDirs = ["jvm/src/main/kotlin"]
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
            }
        }

        jvmTest {
            kotlin.srcDirs = ["jvm/src/test/kotlin"]
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }

        webMain {
            kotlin.srcDirs = ["web/src/main/kotlin"]
            dependencies {
                api group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core-js', version: coroutines_version
            }
        }
		
        webTest {
            kotlin.srcDirs = ["web/src/test/kotlin"]
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }
    }
}